% !TEX root =  master.tex
\chapter{Anhang}
\section{Implementierung der GRS-Kodierung}\label{CodingLst}
\begin{lstlisting}[language=Python, caption={SageMath-Implementierung des Kodieralgorithmus für GRS-Codes}]
#!/usr/bin/env sage
print("### GENERALIZED REED-SOLOMON ENCODING")
print("### This sage script allows you to perform a (non-)systematic Generalized Reed-Solomon encoding over a finite field F_q.")
print("### (C) Roman Wetenkamp, 2023.\n")

# Basic Code properties
q = input("Please type in q, e.g. 2^2: \t\t")
n = int(input("Please type in length n: \t\t"))
k = int(input("Please type in dimension k: \t\t"))

# Generate Galois field
F_q = None
if len(q.split("^")) < 2:
  F_q.<a> = GF(int(q))
elif len(q.split("^")) == 2:
  F_q.<a> = GF(pow(int(q.split("^")[0]), int(q.split("^")[1])))
else:
  print(f"ERROR:\tSorry, unable to work with this q={q}!")
  exit(1)

# GRS Code properties
a = input(f"\nPlease type in evaluation tupel \na = (a_0, a_1, ..., a_{n-1}): \t\t")
a = [F_q(x) for x in a[1:-1].split(",")]

if len(a) != n:
  print(f"ERROR:\tTupel length {len(a)} differs from code length {n}!")
  exit(1)

for element in a:
  if a.count(element) > 1:
    print("ERROR:\tElements of a must be pairwise distinct!")
    exit(1)
  else:
    pass

v = input(f"Please type in weight tupel \nv = (v_0, v_1, ..., v_{n-1}): \t\t")
v = [F_q(x) for x in v[1:-1].split(",")]

if len(v) != n:
  print(f"ERROR:\tTupel length {len(v)} differs from code length {n}!")
  exit(1)

for element in v:
  if v == 0:
    print("ERROR:\tElements of v must not be zero!")
    exit(1)
  else:
    pass

# Generate Generator Matrix G of GRS_k(a, v)
rows = []
i = 0
while i < k:
  row = []
  j = 0
  while j < n:
    row.append(v[j]*(a[j]**i))
    j += 1
  rows.append(row)
  i += 1

G = matrix(F_q, rows)

print(f"\nGenerator matrix: \n{G}")

# Generate systematic Generator Matrix G_S of GRS_k(a, v)
A = G.submatrix(0,0,k,k)
G_S = A.inverse() * G
print(f"\nSystematic form matrix: \n{G_S}")

# Perform Encoding
h = input(f"\nPlease type in message tupel \nh = (h_0, ..., h_{k-1}): \t\t\t")
h = [F_q(x) for x in h[1:-1].split(",")]

if len(h) != k:
  print(f"ERROR:\tTupel length {len(h)} differs from input word length {k}!")
  exit(1)

h_v = matrix(F_q, h)

C = h_v * G
C_S = h_v * G_S

print(f"\nCodeword non-systematic approach:\t{C.rows()[0]}")
print(f"Codeword systematic approach:\t\t{C_S.rows()[0]}")
\end{lstlisting}
\section{Transformieren der Generatormatrix in die systematische Form}
\label{MatrixConversion}
Die folgende Ausarbeitung stellt die Ergebnisse einer Recherche zu Algorithmen dar, die es ermöglichen, aus nicht-systematischen Generatormatrizen systematische zu konstruieren. Sie entstand als Nebenprodukt zu dieser Studienarbeit und ist hier in Gänze dargestellt, da sie weder publiziert noch zitierfähig ist.
\begin{figure}[h!]
\noindent
\centering
\includegraphics[scale=0.75, page=1]{Matrixkonversion/matrixconversion.pdf}
\end{figure}
\pagebreak
\begin{figure}[h!]
\noindent
\centering
\includegraphics[scale=0.75, page=2]{Matrixkonversion/matrixconversion.pdf}
\end{figure}
\pagebreak
\begin{figure}[h!]
\noindent
\centering
\includegraphics[scale=0.75, page=3]{Matrixkonversion/matrixconversion.pdf}
\end{figure}
\cleardoublepage
\section{Implementierung eines GRS-Dekodieralgorithmus}
\label{DecodingLst}
\begin{lstlisting}[language=Python, caption={SageMath-Implementierung eines Dekodieralgorithmus für GRS-Codes}]
#!/usr/bin/env sage
print("### GENERALIZED REED-SOLOMON DECODING")
print("### This sage script allows you to perform a GRS decoding over a finite field F_q.")
print("### (C) Roman Wetenkamp, 2023.\n")

# Basic Code properties
q = input("Please type in q, e.g. 2^2: \t")
n = int(input("Please type in length n: \t"))
k = int(input("Please type in dimension k: \t"))

# Generate Galois field
F_q = None
if len(q.split("^")) < 2:
  F_q.<a> = GF(int(q))
elif len(q.split("^")) == 2:
  F_q.<a> = GF(pow(int(q.split("^")[0]), int(q.split("^")[1])))
else:
  print(f"Sorry, unable to work with this q={q}!")

# Generate GRS-Code
ae = input(f"\nPlease type in evaluation tupel \na = (a_0, a_1, ..., a_{n-1}): \t")
ae = [F_q(x) for x in ae[1:-1].split(",")]

if len(ae) != n:
  print(f"ERROR:\tTupel length {len(ae)} differs from code length {n}!")
  exit(1)

for element in ae:
  if ae.count(element) > 1:
    print("ERROR:\tElements of a must be pairwise distinct!")
    exit(1)
  else:
    pass

v = input(f"Please type in weight tupel \nv = (v_0, v_1, ..., v_{n-1}): \t")
v = [F_q(x) for x in v[1:-1].split(",")]

if len(v) != n:
  print(f"ERROR:\tTupel length {len(v)} differs from code length {n}!")
  exit(1)

for element in v:
  if v == 0:
    print("ERROR:\tElements of v must not be zero!")
    exit(1)
  else:
    pass
 
y = input(f"\nPlease type in word tupel\n(r0,r1,...,r{n-1}) of F_({q})^{n}: \t")
y = [F_q(x) for x in y[1:-1].split(",")]

if len(y) != n:
  print("\nERROR: The provided codeword has not exactly n digits!")
  exit()

# Generate Generator matrix G
rows = []
i = 0
while i < k:
  row = []
  j = 0
  while j < n:
    row.append(v[j]*(ae[j]**i))
    j += 1
  rows.append(row)
  i += 1

G = matrix(F_q, rows)

# Compute Coefficients of the Parity-check matrix H
d = []
for i in range(len(v)):
  d_c = ~v[i] * prod(~(ae[i] - ae[j]) for j in range(0, n) if i != j)
  d.append(d_c)

print(f"\nParity-check Coefficients d: \t{d}")

# Syndrome computation
S = []
for i in range(0, n-k):
  S_j = 0
  for j in range (0, n):
    S_j += y[j] * d[j] * ae[j]**i
  S.append(S_j)

print(f"\nSyndromes: \t\t\t{S}")

if S[1] == 0 and S[1:].count(S[1]) == len(S[1:]):
  print("\n\nINFO: The provided word is a correct codeword and was received error-free!") 
  exit(0)

R.<x> = PolynomialRing(F_q)

# Construct Syndrom-matrix M
mu = (n-k)//2
M = []
for i in range(0, n-k-mu):
  row = []
  for j in range(i, mu+i+1):
    row.append(S[j])
  M.append(row)
print(f"Matrix M_{mu}: \t\t\t{M}")
M = Matrix(R, M)
    
print(f"\nMaximal number of Errors: \t{mu}")

# Key Equation
# S_0 \sigma_e-1 + S_1 \sigma_e-2 + ... + S_e-1 \sigma_0 = 0  
SigCoeff = M.right_kernel().basis()[0]
print(f"\nCoefficients of ELP \sigma: \t{list(SigCoeff)}")

G.<z> = PolynomialRing(F_q)
sigma = sum(G(SigCoeff[i]) * z^(i) for i in range(0, mu+1))

print(f"\nError-locator Polynomial: \t{sigma}")

err_loc = []
for i in range(0, n):
  if sigma(ae[i]) == 0:
    print(f"Found an error at \t\t{i}")
    err_loc.append(i)

# Compute Error Magnitudes E_1, ..., E_v
S_m = []
X_m = []
for i in range(0, n-k):
  S_m.append(S[i])
  X_row = []
  for j in range(0, len(err_loc)):
    X_row.append(d[err_loc[j]] * ae[err_loc[j]]**i)
  X_m.append(X_row)

S_m = Matrix(R, S_m)
X_m = Matrix(R, X_m)

E_m = X_m.solve_right(S_m.T)
E_m = E_m.list()

print(f"\nError magnitudes: \t\t{E_m}")

# Construct Error-Correction Codeword
e = []
for i in range(n):
  if i in err_loc:
    e.append(E_m[err_loc.index(i)])
  else:
    e.append(0)

print(f"Error-Correction Codeword e: \t{e}")

# Correct Errors
c = []
for i in range(n):
  c.append(y[i] - e[i])

print(f"\nCorrected Codeword c: \t\t{c}")
\end{lstlisting}
\section{Implementierung des McEliece-Kryptosystems}\label{McElieceLst}
\begin{lstlisting}[language=Python, caption={SageMath-Implementierung des McEliece-Kryptosystems}]
#!/usr/bin/env sage
import json

# Key generation
def create_keys():
  # Basic Goppa Code Properties
  q = input("Please type in q, e.g. 2^2: \t\t")
  n = int(input("Please type in length n: \t\t"))
  k = int(input("Please type in dimension k: \t\t"))
  t = int(input("Please type in error capacity t: \t"))

  # Generate Galois field
  F_q = None
  if len(q.split("^")) < 2:
          F_q.<a> = GF(int(q))
  elif len(q.split("^")) == 2:
    F_q.<a> = GF(pow(int(q.split("^")[0]), int(q.split("^")[1])))
  else:
          print(f"ERROR:\tSorry, unable to work with this q={q}!")

  # Find a Goppa Polynom
  R.<x> = PolynomialRing(F_q)
  g = R.irreducible_element(t)

  if input("\nShould a specific Goppa polynomial \nof degree t be used? [Y/n] \t\t") in ["Y", "y", ""]:
    g = input("Please enter the Goppa polynomial \nwith x as variable: \t\t\t")
    g = R(g)
    if not g.is_irreducible():
      print(f"\nERROR:\tGiven polynomial is reducible over F_{q}")
      exit()

  print(f"\nGoppa polynomial: \t\t\t{g}")

  # Specifiy set L of all non-roots of g
  # In original McEliece approach, L denotes all elements of F_q
  elements = Set()
  for i in enumerate(F_q):
    _, e = i
    elements = elements.union(Set([e]))

  roots = Set()
  for i in g.roots():
    r, _ = i
    roots = roots.union(Set([r]))

  L = elements.difference(roots)

  print(f"\nSet of all elements: \t\t\t{elements}")
  print(f"Set of all non-root elements: \t\t{L}")

  # Construction of the Parity-check Matrix H
  H_rarr = []
  for i in range(0, k):
    row = []
    for j in L:
      row.append((j**i)/g(j))
    H_rarr.append(row)
  H = Matrix(R, H_rarr)

  print(f"\nParity-Check matrix H: \n{H}")
  
  A = H.submatrix(0,0,k,k)
  H_S = A.inverse() * H

  print(f"\nSystematic Parity-check matrix H: \n{H_S}")

  P = H_S.submatrix(0,k)
  I_n_k = identity_matrix(F_q, n-k)
  G = (-(P.T)).augment(I_n_k)

  print(f"\nGenerator matrix G: \n{G}")

  # Construction of G'
  if input("\nShould a specific scrambling \nk x k-matrix S be used? [Y/n]\t\t") in ["Y", "y", ""]:
    S = input("Please enter the S as a list of lists [[...], [...]]:\t\t")
    print(S.split())
    for i in list(S):
      if i not in (['[', ']', ',', ' ', '^'] + [str(e) for e in elements]):
        print("ERROR:\tUnable to process given S!")
        exit()

    S = Matrix(F_q, eval(S))

  else:
    S = random_matrix(F_q, k, density=1.0)

  print(f"\nScrambling Matrix S: \n{S}")

  if input("\nShould a specific permutation \nn x n-matrix P be used? [Y/n]\t\t") in ["Y", "y", ""]:
    P = input(f"Please enter P as a column-index-list, e.g. [1, ..., {n}]:\t\t")
    for i in list(P):
      if i not in (['[', ']', ',', ' '] + [str(e) for e in range(n+1)]):
        print("\nERROR:\tUnable to process given P!")
        exit()
    P = Permutation(eval(P)).to_matrix()

  else:
    P = Permutations(n).random_element().to_matrix()

  print(f"\nPermutation Matrix P: \n{P}")
  

  # Key generation
  k_pub = S * G * P
  print(f"\nPublic key: \n{k_pub}")
  
  # Save Key files
  if input(f"\nShould the PUBLIC key be saved as 'public_key.json'? [Y/n]\t\t") in ["Y", "y", ""]:
    try:  
      f = open("public_key.json", "w")
      f.write(json.dumps({"q": int(a.multiplicative_order()+1), "n": n, "t": t, "k": k, "key": str(k_pub.rows())}))
      f.close()
    except Exception as e:
      print(e)
      print("\nERROR:\tUnable to write keyfile! Check permissions and try again please.")

  if input(f"\nShould the PRIVATE key be saved as 'private_key.json'? [Y/n]\t\t") in ["Y", "y", ""]:
    try:  
      f = open("private_key.json", "w")
      f.write(json.dumps({"q": int(a.multiplicative_order()+1), "n": n, "t": t, "k": k, "g": str(g), "P": str(P.rows()), "S": str(S.rows()), "G": str(G.rows())}))
      f.close()
    
    except Exception as e:
      print(e)
      print("\nERROR:\tUnable to write keyfile! Check permissions and try again please.")


  return {"q": int(a.multiplicative_order()+1), "n": n, "k": k, "t": t, "g": g, "P": P, "S": S, "G": G, "G'": k_pub}


# Encryption
def encrypt(keys):
  message = input("\nPlease type in the plaintext \nmessage as a list: \t\t\t")
  message = eval(message)

  F_q.<a> = GF(keys["q"])
  
  k_pub = keys["G'"]
  t = keys["t"]
  k = keys["k"]

  n = len(k_pub.columns())
  while len(message) % k != 0:
    message.append(0)
  
  message_blocks = []
  ciphertext_blocks = []
  
  for i in range(0, len(message)//k):
    message_blocks.append(vector(F_q, message[k*i:k+(k*i)]))
  
  for block in message_blocks:
    z = []
    for i in range(n):
      if i < t:
        z.append(1)
      else:
        z.append(0)
    z = vector(F_q, z) * Permutations(n).random_element().to_matrix()
    bk = block * k_pub
        
    encrypted_block = bk + z
    ciphertext_blocks.append(encrypted_block)
  
  return ciphertext_blocks


# Decryption (Placeholder)
def decrypt(keys):
  print("\nERROR:\tDecryption is currently not implemented.")
  exit()


if __name__ == "__main__":
  print("### McEliece CRYPTPOSYSTEM ENCRYPTION")
  print("### This sage script allows you to perform a McEliece cryptosystem encryption.")
  print("### (C) Roman Wetenkamp, 2023.\n")

  # Search for valid key files
  keys = {"q": None, "n": None, "t": None, "k": None, "g": None, "P": None, "S": None, "G": None, "G'": None}
  mode = 0  

  try:
    f = open("public_key.json", "r")
    pubcon = json.loads(f.read())
    keys["q"] = pubcon["q"]
    F_q.<a> = GF(keys["q"])
    R.<x> = PolynomialRing(F_q)
      
    keys["n"] = pubcon["n"]
    keys["t"] = pubcon["t"]
    keys["k"] = pubcon["k"]
    
    keys["G'"] = pubcon["key"]
    keys["G'"] = keys["G'"].replace("^", "**")
    keys["G'"] = Matrix(F_q, eval(keys["G'"], {"a": F_q.gen()}))      
    
    try:
      h = open("private_key.json", "r")
      content = json.loads(h.read())
          
      content["g"] = content["g"].replace("^", "**")
      content["S"] = content["S"].replace("^", "**")
      content["G"] = content["G"].replace("^", "**")

      keys["g"] = R(eval(content["g"], {"a": F_q.gen(), "x": R.gen()}))
      keys["P"] = Matrix(F_q, eval(content["P"]))
      keys["S"] = Matrix(F_q, eval(content["S"], {"a": F_q.gen()}))
      keys["G"] = Matrix(F_q, eval(content["G"], {"a": F_q.gen()}))
    
    except Exception as e:
      print(e)
      print("\nWARNING:\tNo private keyfile found - thus only encryption is possible")
      mode = 1            
  
  except Exception as e:
    print(e)
    print("\nNo valid keyfile found - Key generation is starting ...\n")
    keys = create_keys()
  
  if mode == 0:
    mode = int(input("Please choose a mode \n(1 - Encryption, 2 - Decryption):\t"))
  
  if mode == 1:
    print(f"\nCiphertext blocks:\t\t\t{encrypt(keys)}")
  
  if mode == 2:
    decrypt(keys)

\end{lstlisting}